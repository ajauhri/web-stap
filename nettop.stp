#! /usr/bin/env stap 
# nettop.stp : network usage for different processes
# output format: [process id | parent process id | device name | #packets sent | #packets received | amount of data sent (bytes) | amount of data received (bytes) | executable of target process | time in sec since epoch  

global ifxmit, ifrecv
global ifmerged

probe netdev.transmit
{
  ifxmit[pid(), dev_name, execname(), ppid(), gettimeofday_s()] <<< length
}

probe netdev.receive
{
  ifrecv[pid(), dev_name, execname(), ppid(), gettimeofday_s()] <<< length
}

function print_activity()
{
  #printf("%5s %5s %-7s %7s %7s %7s %7s %-15s\n","PID", "UID", "DEV", "XMIT_PK", "RECV_PK","XMIT_KB", "RECV_KB", "COMMAND")

  foreach ([pid, dev, exec, ppid, ctime] in ifrecv) {
	  ifmerged[pid, dev, exec, ppid, ctime] += @count(ifrecv[pid,dev,exec,ppid,ctime]);
  }
  foreach ([pid, dev, exec, ppid, ctime] in ifxmit) {
	  ifmerged[pid, dev, exec, ppid, ctime] += @count(ifxmit[pid,dev,exec,ppid,ctime]);
  }
  foreach ([pid, dev, exec, ppid, ctime] in ifmerged-) {
    n_xmit = @count(ifxmit[pid, dev, exec, ppid, ctime])
    n_recv = @count(ifrecv[pid, dev, exec, ppid, ctime])
    printf("%d~%d~%s~%d~%d~%d~%d~%s~%lu~\n",
           pid, ppid, dev, n_xmit, n_recv,
           n_xmit ? @sum(ifxmit[pid, dev, exec, ppid, ctime]) : 0,
           n_recv ? @sum(ifrecv[pid, dev, exec, ppid, ctime]) : 0,
           exec, ctime)
  }

  delete ifxmit
  delete ifrecv
  delete ifmerged
}

probe timer.ms(1000), end, error
{
  print_activity()
}

